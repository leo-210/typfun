program = expr

expr = statement | statement ";" expr | "{" expr "}"

statement = if_stmt | let_stmt | fn_decl | logic_or
fn_decl = "fn" IDENTIFIER? "(" params? ")" "{" expr "}"
params = IDENTIFIER ( "," params )?
if_stmt = "if" expr "then" expr "else" expr
let_stmt = "let" IDENTIFIER "=" expr "in" expr

logic_or = logic_and ( "or" logic_or )?
logic_and = equality ( "and" logic_and)?
equality = comparison ( ( "==" | "!=" ) equality )?
comparison = tuple ( ("<" | "<=" | ">" | ">=" ) comparison )?
tuple = term ( "," tuple )?
term = factor ( ( "+" | "-" | "++" ) term)?
factor = unary ( ( "*" | "/" ) factor )?
unary = ( "-" | "!" ) unary | call
call = composition ( "(" args? ")" )?
// here, expr does not consume commas directly, see the implementation
args = expr ( "," args )?
composition = atom ("." composition)?

atom = INTEGER | STRING | "true" | "false" | "(" expr ")" | IDENTIFIER | "(" ")"

